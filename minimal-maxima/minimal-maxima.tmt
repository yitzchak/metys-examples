%!TeX shellEscape = enabled
%!TeX outputDirectory = output
%!TeX copyTargetsToRoot = yes
<|maxima, prompt="(%i{0}) ",
  figure_env_options=H, code_env=minted, code_env_options.breaklines="true",
  stderr_echo=true, stderr_env=Verbatim,
  stderr_env_options.formatcom=\color{red},
  stdout_echo=true, stdout_env=Verbatim,
  math_env=dmath*, split_math=false, wrap_math=false@
\documentclass[12pt,leqno]{article}

\usepackage[T1]{fontenc}
\usepackage{amsmath}
\usepackage{fancyvrb}
\usepackage{float}
\usepackage{graphicx}
\usepackage[outputdir=output]{minted}
\usepackage{xcolor}
\usepackage{breqn}
\usepackage[colorlinks,linkcolor=red]{hyperref}
\usepackage{etoolbox}

\BeforeBeginEnvironment{dmath}{\begin{minipage}{\textwidth}}
\AfterEndEnvironment{dmath}{\end{minipage}}

\addtolength{\topmargin}{-0.5in}
\addtolength{\textheight}{1in}
\addtolength{\footskip}{0.5in}

\setlength{\parskip}{8pt}
\setlength{\parindent}{0pt}

\title{Minimal Maxima\\
{\small Released under the terms of the GNU General Public License, Version 2}}
\author{Robert Dodier}
\date{\today}

\newcommand{\breakingcomma}{%
  \begingroup\lccode`~=`,
  \lowercase{\endgroup\expandafter\def\expandafter~\expandafter{~\penalty0 }}}

\breakingcomma

\begin{document}
\maketitle

\section{What is Maxima?}

Maxima\footnote
{Home page: \url{http://maxima.sourceforge.net} \\
Documents: \url{http://maxima.sourceforge.net/documentation.html} \\
Reference manual: \url{http://maxima.sourceforge.net/docs/manual/en/maxima.html}}
is a system for working with expressions,
such as $x + y$, $\sin (a + b \pi)$, and $u \cdot v - v \cdot u$.

Maxima is not much worried about the meaning of an expression.
Whether an expression is meaningful is for the user to decide.

Sometimes you want to assign values to the unknowns
and evaluate the expression.
Maxima is happy to do that.
But Maxima is also happy to postpone assignment of specific values;
you might carry out several manipulations of an expression,
and only later (or never) assign values to unknowns.

Let's see some examples.

\begin{enumerate}

\item I want to calculate the volume of a sphere.
<|:V: 4/3 * %pi * r^3;|>

\item The radius is 10.
<|:r: 10;|>

\item $V$ is the same as before; Maxima won't change $V$ until I tell it to do so.
<|:V;|>

\item Please re-evaluate $V$, Maxima.
<|:''V;|>

\item I'd like to see a numerical value instead of an expression.
<|:''V, numer;|>

\end{enumerate}

\section{Expressions}

Everything in Maxima is an expression,
including mathematical expressions, objects, and programming constructs.
An expression is either an atom, or an operator together with its arguments.

An atom is a symbol (a name), a string enclosed in quotation marks,
or a number (integer or floating point).

All nonatomic expressions are represented as $\mathit{op}(a_1, \ldots, a_n)$
where $\mathit{op}$ is the name of an operator
and $a_1, \ldots, a_n$ are its arguments.
(The expression may be displayed differently,
but the internal representation is the same.)
The arguments of an expression can be atoms or nonatomic expressions.

Mathematical expressions have a mathematical operator,
such as $+ \; - \; * \; / \; < \; = \; >$
or a function evaluation such as $\mathbf{sin}(x), \mathbf{bessel\_j}(n, x)$.
In such cases, the operator is the function.

Objects in Maxima are expressions.
A list $[a_1, \ldots, a_n]$ is an expression $\mathbf{list}(a_1, \ldots, a_n)$.
A matrix is an expression
\[
\mathbf{matrix}(\mathbf{list}(a_{1,1}, \ldots, a_{1,n}), \ldots, \mathbf{list}(a_{m,1}, \ldots, a_{m,n}))
\]

Programming constructs are expressions.
A code block $\mathbf{block} (a_1, \ldots, a_n)$ is an expression with operator $\mathbf{block}$
and arguments $a_1, \ldots, a_n$.
A conditional statement $\mathbf{if\ } a \mathbf{\ then\ } b \mathbf{\ elseif\ } c \mathbf{\ then\ } d$
is an expression $\mathbf{if}(a, b, c, d)$.
A loop $\mathbf{for\ } a \mathbf{\ in\ } L \mathbf{\ do\ } S$ is an expression similar to $\mathbf{do}(a, L, S)$.

The Maxima function $\mathbf{op}$ returns the operator of a nonatomic expression.
The function $\mathbf{args}$ returns the arguments of a nonatomic expression.
The function $\mathbf{atom}$ tells whether an expression is an atom.

Let's see some more examples.

\begin{enumerate}

\item Atoms are symbols, strings, and numbers.
I've grouped several examples into a list so we can see them all together.
<|restart=true:[a, foo, foo_bar, "Hello, world!", 42, 17.29];|>

\item Mathematical expressions.
<|:[a + b + c, a * b * c, foo = bar, a*b < c*d];|>


\item Lists and matrices.
The elements of a list or matrix can be any kind of expression,
even another list or matrix.
<|restart=true,split=true:
L: [a, b, c, %pi, %e, 1729, 1/(a*d - b*c)];
L2: [a, b, [c, %pi, [%e, 1729], 1/(a*d - b*c)]];
L [7];
L2 [3];
M: matrix ([%pi, 17], [29, %e]);
M2: matrix ([[%pi, 17], a*d - b*c], [matrix ([1, a], [b, 7]), %e]);
M [2][1];
M2 [2][1];
|>

\item Programming constructs are expressions.
$x : y$ means assign $y$ to $x$; the value of the assignment expression is $y$.
$\mathbf{block}$ groups several expressions, and evaluates them one after another;
the value of the block is the value of the last expression.
<|restart=true,split=true:
(a: 42) - (b: 17);
[a,b];
block ([a], a: 42, a^2 - 1600) + block ([b], b: 5, %pi^b);
(if a > 1 then %pi else %e) + (if b < 0 then 1/2 else 1/7);
|>

\item $\mathbf{op}$ returns the operator, $\mathbf{args}$ returns the arguments,
$\mathbf{atom}$ tells whether an expression is an atom.
<|restart=true,split=true:
op (p + q);
op (p + q > p*q);
op (sin (p + q));
op (foo (p, q));
op (foo (p, q) := p - q);
args (p + q);
args (p + q > p*q);
args (sin (p + q));
args (foo (p, q));
args (foo (p, q) := p - q);
atom (p);
atom (p + q);
atom (sin (p + q));
|>

\item Operators and arguments of programming constructs.
The single quote tells Maxima to construct the expression but postpone evaluation.
We'll come back to that later.
<|restart=true,split=true:
op ('(block ([a], a: 42, a^2 - 1600)));
op ('(if p > q then p else q));
op ('(for x in L do print (x)));
args ('(block ([a], a: 42, a^2 - 1600)));
args ('(if p > q then p else q));
args ('(for x in L do print (x)));
|>

\end{enumerate}

\section{Evaluation}

The value of a symbol is an expression associated with the symbol.
Every symbol has a value;
if not otherwise assigned a value, a symbol evaluates to itself.
(E.g., $x$ evaluates to $x$ if not otherwise assigned a value.)

Numbers and strings evaluate to themselves.

A nonatomic expression is evaluated approximately as follows.

\begin{enumerate}
\item Each argument of the operator of the expression is evaluated.
\item If an operator is associated with a callable function, the function is called,
and the return value of the function is the value of the expression.
\end{enumerate}

Evaluation is modified in several ways.
Some modifications cause less evaluation:

\begin{enumerate}
\item Some functions do not evaluate some or all of their arguments,
    or otherwise modify the evaluation of their arguments.
    % Examples: $\mathbf{kill}$, $\mathbf{save}$, $\mathbf{sum}$, $\mathbf{:=}$ (function definition).
\item A single quote $'$ prevents evaluation.
    \begin{enumerate}
    \item $'a$ evaluates to $a$. Any other value of $a$ is ignored.
    \item $'f(a_1, \ldots, a_n)$ evaluates to $f(\mathbf{ev}(a_1), \ldots, \mathbf{ev}(a_n))$.
        That is, the arguments are evaluated but $f$ is not called.
    \item $'(\ldots)$ prevents evaluation of any expressions inside $(...)$.
    \end{enumerate}
\end{enumerate}

Some modifications cause more evaluation:

\begin{enumerate}
\item Two single quotes $''a$ causes an extra evaluation at the time the expression $a$ is parsed.
\item $\mathbf{ev}(a)$ causes an extra evaluation of $a$ every time $\mathbf{ev}(a)$ is evaluated.
\item The idiom $\mathbf{apply}(f, [a_1, \ldots, a_n])$ causes the evaluation
    of the arguments $a_1, \ldots, a_n$ even if $f$ ordinarily quotes them.
\item $\mathbf{define}$ constructs a function definition like $\mathbf{:=}$,
    but $\mathbf{define}$ evaluates the function body while $\mathbf{:=}$ quotes it.
\end{enumerate}

Let's consider how some expressions are evaluated.

\begin{enumerate}

\item Symbols evaluate to themselves if not otherwise assigned a value.
<|restart=true,split=true:
block (a: 1, b: 2, e: 5);
[a, b, c, d, e];
|>

\item Arguments of operators are ordinarily evaluated (unless evaluation is prevented one way or another).
<|restart=true,split=true:
block (x: %pi, y: %e);
sin (x + y);
x > y;
x!;
|>

\item If an operator corresponds to a callable function,
the function is called (unless prevented).
Otherwise evaluation yields another expression with the same operator.
<|restart=true,split=true:
foo (p, q) := p - q;
p: %phi;
foo (p, q);
bar (p, q);
|>

\item Some functions quote their arguments.
Examples: $\mathbf{save}$, $\mathbf{:=}$, $\mathbf{kill}$.
<|restart=true,split=true:
block (a: 1, b: %pi, c: x + y);
[a, b, c];
save ("tmp.save", a, b, c);
f (a) := a^b;
f (7);
kill (a, b, c);
[a, b, c];
|>

\item A single quote prevents evaluation even if it would ordinarily happen.
<|restart=true, split=true:
foo (x, y) := y - x;
block (a: %e, b: 17);
foo (a, b);
foo ('a, 'b);
'foo (a, b);
'(foo (a, b));
|>

\item Two single quotes (quote-quote) causes an extra evaluation at the time the expression is parsed.
<|restart=true,split=true:
diff (sin (x), x);
foo (x) := diff (sin (x), x);
foo (x) := ''(diff (sin (x), x));
|>

\item $\mathbf{ev}$ causes an extra evaluation every time it is evaluated.
Contrast this with the effect of quote-quote.
<|restart=true,split=true:
block (xx: yy, yy: zz);
[xx, yy];
foo (x) := ''x;
foo (xx);
bar (x) := ev (x);
bar (xx);
|>

\item $\mathbf{apply}$ causes the evaluation of arguments even if they are ordinarily quoted.
<|restart=true,split=true:
block (a: aa, b: bb, c: cc);
block (aa: 11, bb: 22, cc: 33);
[a, b, c, aa, bb, cc];
apply (kill, [a, b, c]);
[a, b, c, aa, bb, cc];
kill (a, b, c);
[a, b, c, aa, bb, cc];
|>

\item $\mathbf{define}$ evaluates the body of a function definition.
<|restart=true,split=true:
integrate (sin (a*x), x, 0, %pi);
foo (x) := integrate (sin (a*x), x, 0, %pi);
define (foo (x), integrate (sin (a*x), x, 0, %pi));
|>

\end{enumerate}

\section{Simplification}

After evaluating an expression,
Maxima attempts to find an equivalent expression which is ``simpler.''
Maxima applies several rules which embody conventional notions of simplicity.
For example,
$1 + 1$ simplifies to $2$,
$x + x$ simplifies to $2 x$,
and $\mathbf{sin}(\mathbf{\%pi})$ simplifies to $0$.

However,
many well-known identities are not applied automatically.
For example,
double-angle formulas for trigonometric functions,
or rearrangements of ratios such as $a/b + c/b \rightarrow (a + c)/b$.
There are several functions which can apply identities.

Simplification is always applied unless explicitly prevented.
Simplification is applied even if an expression is not evaluated.

$\mathbf{tellsimpafter}$ establishes user-defined simplification rules.

Let's see some examples of simplification.

\begin{enumerate}

\item Quote mark prevents evaluation but not simplification.
When the global flag $\mathbf{simp}$ is $\mathbf{false}$,
simplification is prevented but not evaluation.

<|restart=true,split=true:
'[1 + 1, x + x, x * x, sin (%pi)];
simp: false;
block ([x: 1], x + x);
simp: true;
|>

\item Some identities are not applied automatically.
$\mathbf{expand}$, $\mathbf{ratsimp}$, $\mathbf{trigexpand}$, $\mathbf{demoivre}$
are some functions which apply identities.

<|restart=true,split=true:
(a + b)^2;
expand (%);
a/b + c/b;
ratsimp (%);
sin (2*x);
trigexpand (%);
a * exp (b * %i);
demoivre (%);
|>

\end{enumerate}

\section{apply, map, and lambda}

\begin{enumerate}

\item $\mathbf{apply}$ constructs and evaluates an expression.
The arguments of the expression are always evaluated (even if they wouldn't be otherwise).
<|restart=true,split=true:
apply (sin, [x * %pi]);
L: [a, b, c, x, y, z];
apply ("+", L);
|>

\item $\mathbf{map}$ constructs and evaluates an expression for each item in a list of arguments.
The arguments of the expression are always evaluated (even if they wouldn't be otherwise).
The result is a list.
<|restart=true,split=true:
map (foo, [x, y, z]);
map ("+", [1, 2, 3], [a, b, c]);
map (atom, [a, b, c, a + b, a + b + c]);
|>

\item $\mathbf{lambda}$ constructs a lambda expression (i.e., an unnamed function).
The lambda expression can be used in some contexts like an ordinary named function.
$\mathbf{lambda}$ does not evaluate the function body.
<|restart=true,split=true:
f: lambda ([x, y], (x + y)*(x - y));
f (a, b);
apply (f, [p, q]);
map (f, [1, 2, 3], [a, b, c]);
apply (lambda ([x, y], (x + y)*(x - y)), [p, q]);
map (lambda ([x, y], (x + y)*(x - y)), [1, 2, 3], [a, b, c]);
|>

\end{enumerate}

\section{Built-in object types}

An object is represented as an expression.
Like other expressions, an object comprises an operator and its arguments.

The most important built-in object types are lists, matrices, and sets.

\subsection{Lists}

\begin{enumerate}

\item A list is indicated like this: $[a, b, c]$.

\item If $L$ is a list, $L[i]$ is its $i$'th element. $L[1]$ is the first element.

\item $\mathbf{map}(\mathit{f}, L)$ applies $\mathit{f}$ to each element of $L$.

\item $\mathbf{apply}(\mathbf{"+"}, L)$ is the sum of the elements of $L$.

\item $\mathbf{for\ } x \mathbf{\ in \ } L \mathbf{\ do \ } \mathit{expr}$
    evaluates $\mathit{expr}$ for each element of $L$.

\item $\mathbf{length}(L)$ is the number of elements in $L$.

\end{enumerate}

\subsection{Matrices}

\begin{enumerate}

\item A matrix is defined like this: $\mathbf{matrix}(L_1, \ldots, L_n)$
    where $L_1, \ldots, L_n$ are lists which represent the rows of the matrix.

\item If $M$ is a matrix, $M[i, j]$ or $M[i][j]$ is its $(i, j)$'th element.
    $M[1,1]$ is the element at the upper left corner.

\item The operator $\mathbf{.}$ represents noncommutative multiplication.
    $M . L$, $L . M$, and $M . N$ are noncommutative products,
    where $L$ is a list and $M$ and $N$ are matrices.

% \item $M\mathbf{\hat{ }\hat{ }}n$ is the noncommutative exponent, i.e., $M . M . \ldots . M$.

\item $\mathbf{transpose}(M)$ is the transpose of $M$.

\item $\mathbf{eigenvalues}(M)$ returns the eigenvalues of $M$.

\item $\mathbf{eigenvectors}(M)$ returns the eigenvectors of $M$.

\item $\mathbf{length}(M)$ returns the number of rows of $M$.

\item $\mathbf{length}(\mathbf{transpose}(M))$ returns the number of columns of $M$.

\end{enumerate}

\subsection{Sets}

\begin{enumerate}

\item Maxima understands explicitly-defined finite sets.
    Sets are not the same as lists; an explicit conversion is needed to change one into the other.

\item A set is specified within curly braces $\{ \}$ like this:
    $\{a, b, c, \ldots\}$ where the set elements are $a, b, c, \ldots$.

\item $\mathbf{union} (A, B)$ is the union of the sets $A$ and $B$.

\item $\mathbf{intersection} (A, B)$ is the intersection of the sets $A$ and $B$.

\item $\mathbf{cardinality} (A)$ is the number of elements in the set $A$.

\end{enumerate}

\section{How to...}

\subsection{Define a function}

\begin{enumerate}

\item The operator $\mathbf{:=}$ defines a function, quoting the function body.

In this example, $\mathbf{diff}$ is reevaluated every time the function is called.
The argument is substituted for $x$ and the resulting expression is evaluated.
When the argument is something other than a symbol, that causes an error:
for $\mathbf{foo} (1)$ Maxima attempts to evaluate $\mathbf{diff} (\mathbf{sin}(1)^2, 1)$.
<|restart=true,split=true:
foo (x) := diff (sin(x)^2, x);
foo (u);
foo (1);
|>

\item $\mathbf{define}$ defines a function, evaluating the function body.

In this example, $\mathbf{diff}$ is evaluated only once (when the function is defined).
$\mathbf{foo} (1)$ is OK now.
<|restart=true,split=true:
define (foo (x), diff (sin(x)^2, x));
foo (u);
foo (1);
|>

\end{enumerate}

\subsection{Solve an equation}
<|restart=true,split=true:
eq_1: a * x + b * y + z = %pi;
eq_2: z - 5*y + x = 0;
s: solve ([eq_1, eq_2], [x, z]);
length (s);
[subst (s[1], eq_1), subst (s[1], eq_2)];
ratsimp (%);
|>

\subsection{Integrate and differentiate}

$\mathbf{integrate}$ computes definite and indefinite integrals.
<|restart=true,split=true:
integrate (1/(1 + x), x, 0, 1);
integrate (exp(-u) * sin(u), u, 0, inf);
assume (a > 0);
integrate (1/(1 + x), x, 0, a);
integrate (exp(-a*u) * sin(a*u), u, 0, inf);
integrate (exp (sin (t)), t, 0, %pi);
'integrate (exp(-u) * sin(u), u, 0, inf);
|>

$\mathbf{diff}$ computes derivatives.
<|restart=true,split=true:
diff (sin (y*x));
diff (sin (y*x), x);
diff (sin (y*x), y);
diff (sin (y*x), x, 2);
'diff (sin (y*x), x, 2);
|>

\subsection{Make a plot}

$\mathbf{plot2d}$ draws two-dimensional graphs.
<|restart=true,split=true:
plot2d (exp(-u) * sin(u), [u, 0, 2*%pi]);
plot2d ([exp(-u), exp(-u) * sin(u)], [u, 0, 2*%pi]);
xx: makelist (i/2.5, i, 1, 10);
yy: map (lambda ([x], exp(-x) * sin(x)), xx);
plot2d ([discrete, xx, yy]);
plot2d ([discrete, xx, yy], [style, points]);
|>

See also $\mathbf{plot3d}$.

\subsection{Save and load a file}

$\mathbf{save}$ writes expressions to a file.
<|restart=true,split=true:
a: foo - bar;
b: foo^2 * bar;
save ("my.session", a, b);
save ("my.session", all);
|>

$\mathbf{load}$ reads expressions from a file.
<|split=true:
load ("my.session");
a;
b;
|>

See also $\mathbf{stringout}$ and $\mathbf{batch}$.

\section{Maxima programming}

% dynamic scope
% argument-quoting and argument-evaluating functions
% directory organization: src, tests, share, doc

There is one namespace, which contains all Maxima symbols.
There is no way to create another namespace.

All variables are global unless they appear in a declaration of local variables.
Functions, lambda expressions, and blocks can have local variables.

The value of a variable is whatever was assigned most recently,
either by explicit assignment or by assignment of a value to a local variable
in a block, function, or lambda expression.
This policy is known as {\it dynamic scope}.

If a variable is a local variable in a function, lambda expression, or block,
its value is local but its other properties
(as established by $\mathbf{declare}$)
are global.
The function $\mathbf{local}$ makes a variable local with respect to all properties.

By default a function definition is global,
even if it appears inside a function, lambda expression, or block.
$\mathbf{local}(f), f(x) \mathbf{\ :=\ } \ldots$ creates a local function definition.

$\mathbf{trace}(\mathit{foo})$ causes Maxima to print an message when the function $\mathit{foo}$
is entered and exited.

Let's see some examples of Maxima programming.

\begin{enumerate}

\item All variables are global unless they appear in a declaration of local variables.
Functions, lambda expressions, and blocks can have local variables.

<|restart=true,split=true:
(x: 42, y: 1729, z: foo*bar);
f (x, y) := x*y*z;
f (aa, bb);
lambda ([x, z], (x - z)/y);
apply (%, [uu, vv]);
block ([y, z], y: 65536, [x, y, z]);
|>

\item The value of a variable is whatever was assigned most recently,
either by explicit assignment or by assignment of a value to a local variable.

<|restart=true,split=true:
foo (y) := x - y;
x: 1729;
foo (%pi);
bar (x) := foo (%e);
bar (42);
bar ('u);
|>

\end{enumerate}

\section{Lisp and Maxima}

% symbols, $ and ?
% defining an argument-evaluating function in lisp
% defining an argument-quoting function in lisp
% calling a function defined in maxima from lisp
% useful lisp fcns: meval, simplifya, displa

The construct {\bf :lisp} $\mathit{expr}$ tells the Lisp interpreter
to evaluate $\mathit{expr}$.
This construct is recognized at the input prompt and in files processed by $\mathbf{batch}$,
but not by $\mathbf{load}$.

The Maxima symbol $\mathbf{foo}$ corresponds to the Lisp symbol \$foo,
and the Lisp symbol foo corresponds to the Maxima symbol $\mathbf{?foo}$.

{\bf :lisp} $\mathrm{(}\mathbf{defun\ } \mathrm{\$foo\ (a)\ (\ldots))}$
defines a Lisp function $\mathrm{foo}$ which evaluates its arguments.
From Maxima, the function is called as $\mathbf{foo}(a)$.

{\bf :lisp} $\mathrm{(}\mathbf{defmspec\ } \mathrm{\$foo\ (e)\ (\ldots))}$
defines a Lisp function $\mathbf{foo}$ which quotes its arguments.
From Maxima, the function is called as $\mathbf{foo}(a)$.
The arguments of $\mathrm{\$foo}$ are $(\mathbf{cdr\ } e),$
and $(\mathbf{caar\ } e)$ is always $\mathrm{\$foo}$ itself.

From Lisp, the construct $(\mathbf{mfuncall\ '\$}\mathrm{foo\ }a_1 \ldots a_n)$
calls the function $\mathbf{foo}$ defined in Maxima.

Let's reach into Lisp from Maxima and vice versa.

\begin{enumerate}

\item The construct {\bf :lisp} $\mathit{expr}$ tells the Lisp interpreter
to evaluate $\mathit{expr}$.
<|restart=true,split=true:
(aa + bb)^2;
:lisp $%
|>

\item {\bf :lisp} $\mathrm{(}\mathbf{defun\ } \mathrm{\$foo\ (a)\ (\ldots))}$
defines a Lisp function $\mathbf{foo}$ which evaluates its arguments.
<|restart=true,split=true:
:lisp (defun $foo (a b) `((mplus) ((mtimes) ,a ,b) $%pi))
(p: x + y, q: x - y);
foo (p, q);
|>

\item {\bf :lisp} $\mathrm{(}\mathbf{defmspec\ } \mathrm{\$foo\ (e)\ (\ldots))}$
defines a Lisp function $\mathbf{foo}$ which quotes its arguments.
<|restart=true,split=true:
to_lisp ();
(defmspec $bar (e) (let ((a (cdr e))) `((mplus) ((mtimes) ,@a) $%pi)))
(to-maxima)
(p: x + y, q: x - y);
bar (p, q);
bar (''p, ''q);
|>

\item From Lisp, the construct $(\mathbf{mfuncall\ '\$}\mathrm{foo\ }a_1 \ldots a_n)$
calls the function $\mathbf{foo}$ defined in Maxima.

<|restart=true,split=true:
blurf (x) := x^2;
:lisp (displa (mfuncall '$blurf '((mplus) $grotz $mumble)))
|>
\end{enumerate}

\end{document}
|>
